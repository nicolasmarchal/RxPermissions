apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionName rootProject.ext.libraryVersion
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions.unitTests.includeAndroidResources = true
    testOptions.unitTests.all {
        // unitTests.returnDefaultValues = true
        // Always show the result of every unit test, even if it passes.
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
    namespace "com.github.tbruyelle.rxpermissions"
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation rootProject.ext.rxJava
    implementation rootProject.ext.androidXFragment
    implementation rootProject.ext.androidXAnnotation
    implementation rootProject.ext.androidXAppcompat

    testImplementation 'junit:junit:4.13'
    testImplementation rootProject.ext.mockito
    testImplementation rootProject.ext.robolectric
    testImplementation rootProject.ext.khronosOpenGLApi
}

publishing {
    publications {
        release(MavenPublication) {
            afterEvaluate {
                from components.release
            }

            groupId = 'com.github.tbruyelle'
            artifactId = 'RxPermissions'
            version = android.defaultConfig.versionName

            pom {
                name = 'RxPermissions'
                description = 'A wrapper for Android 6.0 permissions'
                url = 'https://github.com/tbruyelle/RxPermissions'
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'tbruyelle'
                        name = 'Thomas Bruyelle'
                        email = 'thomas.bruyelle@gmail.com'
                    }
                }
            }
        }
    }
}
